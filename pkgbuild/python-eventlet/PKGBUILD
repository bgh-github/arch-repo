# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-eventlet
_pyname=eventlet
pkgver=0.33.3
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq')
optdepends=(
  'python-psycopg2: non-blocking PostgreSQL support'
  'python-pyopenssl: non-blocking SSL support'
  'python-httplib2: non-blocking HTTP support'
  'python-pyzmq: non-blocking ZeroMQ support'
  'python-dnspython: non-blocking DNS support'
)
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz
        python310.patch)
sha512sums=('b2e1818c7b6134be4020d2b4315bbb3db795960df76da08a490e0d952549eafb477800c884e7a99493146c1ac082aed5fb87c7e2882ee468ea42b5cc26b8517f'
            '54038895167606bc4cc149b0f3545d5a845b396852d8621b8f09fd9c88c34befebfadba35eacc9af42dfbd113f703e7e3f6c1a8c5c0138a57e3cc8bfa9604f31')
b2sums=('19bcd7a5adc2ef24f2637bfd9b19e68a9a268ebb7791cc9c419d9da88e5860c4cbbf6ecbaa0f3c300246584082622e6dab7ab75374f7d54f3c4a65661e22dbfd'
        '02ed18a3687c892716752148eab765312173912938a83633c1bd93f9445960ed585aad2be78fb75c545d740eab28c42da811874dd4dd65009b2ae6af6b22607a')

prepare() {
  cd ${_pyname}-${pkgver}
  # https://github.com/eventlet/eventlet/issues/730
  # https://github.com/eventlet/eventlet/issues/739
  patch -Np1 -i ../python310.patch
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -C doc text
}

# check() {
#   cd ${_pyname}-${pkgver}
#   nosetests -sv tests -e test_018b_http_10_keepalive_framing # fails on build.a.o
# }

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
